hdnl Auto generated by ymake
dnl Process this file with autoconf to produce a configure script

dnl AC_INIT(kibot, 0.0.8) -- this is in the "new" autoconf
dnl for now, do the following three lines instead:
AC_INIT(kibot.spec)
PACKAGE_TARNAME=kibot
PACKAGE_VERSION=0.0.12

dnl Package information.
DATE=2003/09/25
PACKAGE=$PACKAGE_TARNAME
VERSION=$PACKAGE_VERSION
AC_SUBST(VERSION)
AC_SUBST(PACKAGE)
AC_SUBST(DATE)

dnl Default prefix.
AC_PREFIX_DEFAULT(/usr)

dnl top_builddir.
top_builddir=`pwd`
AC_SUBST(top_builddir)

AC_SUBST(docdir)
AC_SUBST(mandir)
AC_SUBST(sysconfdir)

dnl Checks for programs.
AC_PROG_INSTALL

AC_MSG_CHECKING(for --with-python)
AC_ARG_WITH(python, [--with-python=BIN       Path to specific Python binary],
[
  AC_SUBST(PYTHON_BIN)
  PYTHON_BIN="$withval"
  AC_MSG_RESULT($PYTHON_BIN)
],
AC_MSG_RESULT(no))

# check for Python executable
if test -z "$PYTHON_BIN"; then
  AC_PATH_PROG(PYTHON_BIN, python)
  if test -z "$PYTHON_BIN"; then
    AC_MSG_ERROR(python binary not found in path)
  fi
fi

# find out python version
AC_MSG_CHECKING(Python version)
PyVERSION=`$PYTHON_BIN -c ['import sys; print sys.version[:3]'`]
PyMAJVERSION=`$PYTHON_BIN -c ['import sys; print sys.version[:1]'`]
PyMINVERSION=`$PYTHON_BIN -c ['import sys; print sys.version[2:3]'`]
AC_MSG_RESULT($PyVERSION)

# make sure Python is version 2
if test "$PyMAJVERSION" -lt "2" || \
   (test "$PyMAJVERSION" -eq "2" && test "$PyMINVERSION" -lt "2"); then
  AC_MSG_ERROR([This version of kibot requires at least Python version 2.2.  
The one you have seems to be $PyVERSION.  You can specify an alternate python 
by using (for example) --with-python=/usr/bin/python2.2])
fi

# find out compiled in install prefix
AC_MSG_CHECKING(Python install prefix)
PyEXEC_INSTALLDIR=`$PYTHON_BIN -c "import sys; print sys.exec_prefix"`
AC_MSG_RESULT($PyEXEC_INSTALLDIR)


# this is where the Python libraries will get installed if 
# --with-python-dirs is set
AC_SUBST(PY_STD_LIB)
PY_STD_LIB=${PyEXEC_INSTALLDIR}/lib/python${PyVERSION}

# otherwise, use this (relative to $libdir)
AC_SUBST(PY_PREFIX_LIB)
PY_PREFIX_LIB=$libdir/python${PyVERSION}

# now figure out which of these to use.  On most systems with normal defaults,
# these will be the same anyway.
AC_SUBST(PY_MODULE_DIR)
AC_MSG_CHECKING(for --with-python-dirs)
AC_ARG_WITH(python-dirs, [--with-python-dirs      Put modules in python's site-packages dir],
[
  PY_MODULE_DIR=$PY_STD_LIB
  AC_MSG_RESULT(yes)
],
[
  PY_MODULE_DIR=$PY_PREFIX_LIB
  AC_MSG_RESULT(no)
])

# debian does not ship xml.sax with the python package
AC_MSG_CHECKING(for xml.sax)
if ($PYTHON_BIN -c 'import xml.sax' > /dev/null 2&>1); then
  AC_MSG_RESULT(yes)
else
  AC_MSG_RESULT(no)
  AC_MSG_ERROR([This version of kibot requires the xml.sax python module,
which was not found on your system.])
fi

dnl Checks for libraries.

dnl Checks for headers.

dnl Checks for typedefs, structures, and compiler characteristics.

dnl Checks for library functions.

dnl AC_CONFIG([Makefile bin/Makefile kibot/Makefile modules/Makefile])
dnl AC_CONFIG([pymod/Makefile doc/Makefile tools/Makefile])
dnl AC_CONFIG([bin/kibot],         [chmod +x bin/kibot])
dnl AC_CONFIG([bin/kibot-control], [chmod +x bin/kibot-control])
AC_OUTPUT_COMMANDS(chmod +x bin/kibot)
AC_OUTPUT_COMMANDS(chmod +x bin/kibot-control)
AC_OUTPUT(Makefile bin/Makefile kibot/Makefile modules/Makefile \
          pymod/Makefile doc/Makefile tools/Makefile \
          bin/kibot bin/kibot-control)
